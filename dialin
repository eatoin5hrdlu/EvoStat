#!/usr/bin/xpce
:- set_prolog_flag(double_quotes,codes).
:- dynamic show_conversation/1.
:- dynamic arduino_windows/1.
arduino_windows(0).

:- pce_begin_class(dialin, dialog, "Conversation").

variable(socket, any, get, "Bluetooth Socket").

initialise(Self, Label:[name], Socket:[any]) :->
	"Have a short conversation over Bluetooth"::
        send_super(Self, initialise(Label)),
	send(Self, slot, socket, Socket),
	send(Self?frame, icon, bitmap('./evo.xpm')), % LOGO
        new(Diatxt, text_buffer),
        new(Editor, editor(Diatxt)),         % Emacs editor
	
	% Font size affects the text window size!
	% So do this before setting width/height
	new(Font12, font(roman, bold, 11)),
	send(Editor, font, Font12),

	send(Editor?bindings, function, 'RET', message(Self,enter)),
	screen(DW,DH,WF,HF,_),
	MyW is 54+(DW*WF/100)/2,
	MyH is (DH*HF/100)/2,
	get(Editor,  area, Area),                     % Resize
	send(Area, set(width:=MyW,height:=MyH)),
	get(Editor, font, Font),
	plog(editorFont(Font)),
	send(Self, append, Editor),
	updates_off,
	random(50,100,YPos),
	random(300,500,XPos),
	send_super(Self, open, point(XPos,YPos)).

resize(Self,MyW,MyH) :-> "Resize the Editor Text Window"::
	get(Self, member, editor, Editor),
	get(Editor,  area, Area),
	send(Area, set(width:=MyW,height:=MyH)).

updates_off :-
	    arduino_windows(N),
	    plog(gui(off,windows(N))),
	    retract(arduino_windows(N)),
	    (N =:= 0 -> send(@gui,stopped) ; true),
	    NN is N + 1,
	    assert(arduino_windows(NN)).

updates_on :- arduino_windows(NN),
	      N is NN - 1,
	      retract(arduino_windows(NN)),
	      assert(arduino_windows(N)),
	      plog(gui(on,windows(N))),
	      (N =:= 0 -> send(@gui, started) ; true ).

enter(Self) :-> "Stateful question/answer window"::
	retract(show_conversation(Self)),
	!,
	send(Self, clear).

enter(Self) :-> "Send or clear window (alternately)"::
	chain_list(Self?graphicals, [Editor|_]),      % Contents of 
	get(Editor?text_buffer?contents, value, Raw), % Edit pane
	normalize_space(atom(Cmd),Raw),
	( Cmd = reset
	 -> send(Self,reset),
	    send(Self,clear)
	;   talk(Self, Cmd, Editor)
	).

% A real hack
translate0(0'4, 0'h).
translate0(0'8, 0'l).
translate0(0'9, 0'm).
% To show id code as: h(ost), m(odule), or l(agoon)
translate( String, ResultString) :-
    string_codes(String,Codes),
    append("id(10",[X|Rest],Codes),
    translate0(X,C),
    !,
    append("id(",[C|Rest],ResultCodes),
    string_codes(ResultString,ResultCodes).
translate(String,ResultString) :-
    string_codes(String,Codes),
    nocr(Codes,NewCodes),
    string_codes(ResultString,NewCodes).

nocr([],[]).
nocr([13|T],NT) :-
	!,
	nocr(T,NT).
nocr([H|T],[H|NT]) :-
	nocr(T,NT).

talk(Self, Cmd, Editor) :-
	get(Self, socket, Socket),
	plog(before(Socket,Cmd)),
	bluetalk(Socket, Cmd, Msg0),
	translate(Msg0,Msg),
	plog(after(Socket,Msg)),
	( ( Msg=[] ; string_length(Msg,0) )
	     -> get_super(Self, label, Whoami),
		send(Editor?text_buffer, append, '\nreconnecting('),
		send(Editor?text_buffer, append, Whoami),
		send(Editor?text_buffer, append, ').'),
		send(@Whoami, connect),
		get(@Whoami, socket, NewSocket),
		send(Self, slot, socket, NewSocket)
             ;  send(Editor?text_buffer, append, '\n'),
		send(Editor?text_buffer, append, Msg)
	),
	assert(show_conversation(Self)),
	send(Self, keyboard_focus, Editor).

clear(Self) :->
     chain_list(Self?graphicals, [Editor|_]),
     send(Editor?text_buffer, clear),
     send(Self, keyboard_focus, Editor).

reset(Self) :->
     get(Self, slot, socket, Sock),
     bt_areset(Sock).

% Program crashes if you don't pass unlink on to super
unlink(Self) :->
	     updates_on, % We had shut off EvoStat updates
	     send_super(Self,unlink).

:- pce_end_class.





