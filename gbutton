#!/usr/bin/xpce
:- use_module(library(time)).
:- use_module(library(pce)).
:- use_module(library(process)).
:- use_module(library(charsio)).
:- use_module(library(helpidx)).
:- use_module(library(lists)).
:- use_module(library(ctypes)).
:- use_module(library(filesex)).
% Arduino controllers return type as a single character id(char),
id_type(0's, supply).
id_type(0'c, cellstat).
id_type(0'h, cellstat).
id_type(0'a, sampler).
id_type(0'l, lagoon).
id_type(0'2, lagoon).

mix_colors(   _, red,    red) :- !.
mix_colors( red,   _,    red) :- !.
mix_colors(   C,   C,      C) :- !.
mix_colors(   _,blue, purple) :- !.
mix_colors(blue,   _, purple) :- !.
mix_colors(   _,  _,  orange).

compatible(T1,T2) :-
	nonvar(T1),nonvar(T2),
        functor(T1,Name,Arity),functor(T2,Name,Arity).
compatible(T1,T2) :-
	nonvar(T2),functor(T2,Name,Arity),
	writeln(parse_error(expected(Name/Arity),got(T1))),
	fail.

report_error(Reply) :-
	( catch(atom_to_term(Reply,converse_Error(X),_),_,fail)
	-> plog(error(X))
	;  true
	).
%
% Dec10 option is cool, it will plow through nonsense until it 
% reads a valid term, or hits end_of_file which is a Term!
% ( end_of_file means end_of_atom in this case )
%
parse_reply(@nil, _) :- !.
parse_reply(Reply, Term) :-
	read_term_from_atom(Reply,Term,[syntax_error(dec10)]).

parse_reply_arg1(@nil, _, _) :- !.
parse_reply_arg1(Reply, Functor, Arg1) :-
	read_term_from_atom(Reply,Term,[syntax_error(dec10)]),
	Term =.. [Functor,Arg1|_].

getIPfromMAC(_,_) :- fail.

snapshot(Path, FileName) :-
	concat_atom([Path,'web/phagestat.jpg'],FileName).

:- dynamic tog/1.
toggle('mypic1.jpg','mypic2.jpg') :- tog(2), retract(tog(2)),assert(tog(1)).
toggle('mypic2.jpg', 'mypic1.jpg') :- tog(1), retract(tog(1)),assert(tog(2)).

wget( OtherFile,
      '/usr/bin/wget',
      ['-q', '-O',File,
      'http://192.168.2.117:88/cgi-bin/CGIProxy.fcgi?cmd=snapPicture2&usr=scrapsec&pwd=lakewould']):-
      toggle(File,OtherFile).


:- pce_begin_class(snapshot, window).

variable(tmpbitmap, any, both, "Bitmap memory mgmt").

initialise(Self, Label:[name]) :->
	  "Initialise the image area"::
        send_super(Self, initialise(Label)),
        send_super(Self, alignment, center),
	param(imageSize(Ysize,Xsize)),
	send(Self, size, size(Ysize,Xsize)),
	new(B, bitmap('./web/phagestat.jpg')),
	send(Self, slot, tmpbitmap, B),
	send(Self, display, B).

update(Self) :->
	"Must copy image to new filename to avoid caching"::
      statistics(snap1),
      writeln(updating(snapshot)),
      R is random(10000),
      concat_atom(['./web/phagestat',R,'.jpg'],NewFile),
      copy_file('./web/phagestat.jpg',NewFile),
      new(NB, bitmap(NewFile)),
      plog(newbitmap),
      get(Self, tmpbitmap, OB),
      statistics(snap2),
      send(Self, slot, tmpbitmap, NB),          % Save the new bitmap
      send(Self, clear),
      send(Self, display, NB),
      catch(free(OB),FEx,(plog(freeing(FEx,failed)),true)),
      plog(freed(bitmap)),
      statistics(snap3),
      delete_file(NewFile),
      plog(updated(snapshot)).

closesocket(_Self) :-> true.

:- pce_end_class.  % snapshot

:- pce_begin_class(ebutton, button).

variable(myname,           name,       both, "Component ID"             ).
variable(socket,            any,       both, "Bluetooth/IP Socket"      ).
variable(reply,            name,       both, "Last Message from Device" ).
variable(l,                 int := 29, both, "Percent Full"             ).
variable(tl,                int := 31, both, "Target Level"             ).
variable(fr,               real := 1.5, both,"Flow  Rate"               ).
variable(tf,               real := 3.5, both, "Target Flow Rate"        ).
variable(minfi,            int := 400,  both, "Min Input valve ms"      ).
variable(maxfi,            int := 1400, both, "Max Input valve ms"      ).
variable(minfo,            int := 400,  both, "Min Output valve ms"     ).
variable(maxfo,            int := 1400, both, "Max Output valve ms"     ).
variable(levelUnits,       name := 'mL', both, "% or mL (usually)"      ).
variable(turbidityUnits,   name := 'OD(600)', both,"Optical Density"      ).
variable(temperatureUnits, name := 'C', both, "Degrees Centigrade"      ).
variable(flowUnits,        name := 'Vol/hr', both, "Vol/hr"             ).
variable(fluxUnits,        name := 'lux',    both, "lux"                ).
variable(rlUnits,          name := 'RLU',    both, "rlu"                ).

initialise(Self, Label:[name]) :->
	"Initialise the button and connect to device"::
        send_super(Self, initialise(Label)),
        send(Self, myname, Label),
	retractall(changed(Self,_,_)),
	send(Self, slot, l,  30),
	send(Self, slot, socket, @nil),
	new(Code,  message(Self, show_conversation)),

	send(Self, recogniser, click_gesture(right,'',single,Code)).

update(_Self) :-> % true. % Anything that all buttons do
      statistics(ebutton0).
	
show_conversation(@Name) :->
        get(@Name, socket, Socket),
	(Socket == @nil
	    -> send(@Name, connect)
	    ; new(_, dialin(Name,Socket))
	).

reset(Self) :-
     get(Self, socket, Socket),
     component(Name,_Type,Self),
     plog(resetting(Name,Socket)),
     sleep(1),
     bt_areset(Socket),
     plog(reset(Name,Socket)), % pause after RESET
     sleep(3).
	
compute(_) :-> true.

% Shape is defined as a percentage of the screen
shape(Self, W:[int], H:[int]) :->
	window_percent(W,H,MyWidth,MyHeight),
%	writeln(shape(Self,MyWidth,MyHeight)),
	send(Self, size, size(MyWidth,MyHeight)).

od(Self, OD600:[real]) :->
	 Turb is integer(OD600*1000),
	 send(Self, tb, Turb).

temp(Self, Temp:[int]) :->
	 send(Self, tt, Temp),
         plog(tempsetting(Self,Temp)).

% Cellstat, Lagoon, and Autosampler Bluetooth Communications
%
% Wedged Arduinos give null responses, but just what constitutes
% a null response when there are SIX kinds of nuthin!
%
nullreply("") :- !,writeln(reply(doublequotes)).
nullreply([]) :- !,writeln(reply(emptylist)).
nullreply(['']) :- !,writeln(reply(emptyatominalist)).
nullreply('') :- !,writeln(reply(emptyatom)).
nullreply(``) :- !,writeln(reply(emptystring)).
nullreply(AS) :- atom_string('',AS),!,writeq(reply(atomstring)).
%nullreply(NR) :- writeq(reply(good,NR)),fail.

replyok(send_failed) :- !, fail.
replyok(R) :- \+ nullreply(R),!.
replyok(R) :- plog(replyok(R,failed)),fail.

% Get the Bluetooth socket (first time or replace old)
newsocket(Self) :-
    (component(Name,_Type,Self)->true;plog(no_component(Name))),
    get(Self, socket, Old),
    (Old = @nil -> true ; bt_close(Old)),
    (bt_device(Name, Addr)->true;plog(no_bluetooth_device(Name)),fail),
    bt_socket(Addr, New),
    (New >= 0 -> true ; plog(bluetooth_socket_failed(Name,Addr))),
    send(Self, slot, socket, New),
    send(Self, colour, colour(darkgreen)).

good_socket(Self, Socket) :-
	get(Self, socket, S),
        check_socket(Self, S, Socket),
	integer(Socket),
	Socket >= 0.

closesocket(Self) :->
    get(Self, socket, Old),
    (Old = @nil -> true ; bt_close(Old)).

% This predicate implements error handling on Bluetooth communications
% It sets choicepoints which must be CUT by calling predicate
% It sends a RESET and retries after the first failure,
% Creates a new socket, sends RESET, and retries after the second failure,
% Finally it will report failure and fail.

check_socket(_,    S, S) :- plog(' check_socket'(norm)).
check_socket(Self, S, S) :- reset(Self), plog('  check_socket'(reset)).
check_socket(Self, _, New) :-  % Get new/(or the very first) socket
        plog('   check_socket'(new)),
	newsocket(Self),
	reset(Self),
	get(Self, socket, New).
check_socket(Self, _, _) :-> 
        plog('    check_socket'(fail)),
	send(Self,colour,colour(red)),
	fail.
	
converse(Self,_) :-> "Bluetooth Serial Communication"::
        component(Name, _, Self),
        \+ bt_device(Name, _Addr),
	plog(no_bluetooth_device(Name)),
        !. % Do nothing because no current valid device
	
converse(Self, Cmd) :-> "Bluetooth Serial Communication"::
	good_socket(Self, Socket),     % nondet
	bluetalk(Socket, Cmd, Reply),  % det if succeeds
	plog(bluetalk_returned(Socket,Cmd,Reply)),
	replyok(Reply),
	statistics(converse1),
	send(Self,slot,reply,Reply),
	statistics(converse2),
	!.   % check_socket/2 created a choice point for reconnect/reset

converse(Self, Cmd) :-> "Bluetooth Serial Communication"::
        component(Name, _, Self),
	plog(converse(Name,Cmd,failed)),
	fail.

connect(Self) :->
	plog(connect_asking_for_id),
	send(Self, converse, id),
	plog(reply(id,Reply)),
	get(Self, reply, Reply),
	plog(reply(id,Reply)),
	parse_reply(Reply,Term),
	nonvar(Term),
	Term =.. [_,ID],
	id_type(ID,Type),
	plog(connected(Type)),
	!.

connect(Self) :->
	get(Self, myname, MyName),
	plog(failed(bt_socket(MyName))),
	send(Self, slot, socket, @nil),
	send(Self,colour,colour(red)).

%
% These utilities inside class simplify the code
% generated from the interface
%
% updateArduino(Obj)     synchronize GUI <-> Arduino
% sendArduino(Obj,Cmd)   move values GUI -> Arduino
% getArduino(Obj,Cmd)    move values Arduino -> GUI
% updateLabel(T,N,-Label) Label showing curent values

sendArduino(Obj,Cmd) :-
	get(Obj,Cmd,Value),
	concat_atom([Cmd,Value],CmdVal),
	send(Obj, converse, CmdVal).
sendArduino(_Obj,Cmd) :-
        plog(sendArduino(Cmd,failed)).

getArduino(Obj,Cmd) :-
        send(Obj,converse,Cmd),
	get(Obj,reply, Reply),
	parse_reply_arg1(Reply, N2, V2),
	nonvar(N2),
	nonvar(V2),
	send(Obj, N2, V2).
getArduino(_Obj,Cmd) :-
    plog(getArduino(Cmd,failed)).

% Get r/o and push r/w values to Device
updateArduino(Obj, ReadOnly) :-
    findall(P,retract(changed(Obj,P,_)),Ps),
    maplist( send(Obj,pull), ReadOnly),
    maplist( send(Obj,push), Ps),
    ( Ps = [] -> true; send(Obj,converse, s) ),
    send(Obj, check_level).  % Non PID level control

relabel(Obj) :->
    component(MyName,Type,Obj),
    ( offline(Obj) -> C = darkred ; C = darkgreen),
    send(Obj,colour,colour(C)),
    retractall(html_syntax), % labels with nl, not HTML
    label(Type,MyName,NewLabel,[]),
    flatten(NewLabel,LabelAtoms),
    concat_atom(LabelAtoms,NewAtomicLabel),
    send_super(Obj, label, NewAtomicLabel).

:- pce_end_class. % ebutton

paint(Things, Color) :-
	send(@gui?graphicals, for_all,
		if(message(@arg1,instance_of,Things),
				message(@arg1,colour,colour(Color)))).

leak_color(=, orange).
leak_color(>,   blue).
leak_color(<,    red).

leak_type(=,    maybe).
leak_type(<, definite).


% To enable leak reporting an hour after last report:
% ?- reenable_reporting(leak, 3600)

:- dynamic reported/4. % Absence of this fact enables reporting

enable_reporting(Type, Elapsed) :-
    reported(Type,Severity,ReportTime,_), % There was a report
    get_time(NowFloat),
    NowFloat > ReportTime + Elapsed,      % But it has expired
    retract(reported(Type,Severity,ReportTime,_)), % Remove it
    !.
enable_reporting( _, _).
%
% Reporting is imediate when condition occurs, but further
% reporting of the same condition is suppressed for 1 hour(3600 sec
%)
check_condition(leak) :-
	enable_reporting(leak, 3600), % and re-enable after 1 hour
	component(_,cellstat,Obj),
	get(Obj, w, N),
	plog('CHECKING LEAK'(N)),
	get_time(NowFloat),
	compare_delta(Op,N,600,100),
	( ( leak_type(Op,Type),
	    N =\= 500,
	    \+ reported(leak, Type, _, _) )
	-> leak_type(Op,Type),
	   assert(reported(leak,Type,NowFloat,N)),
	   report,
	   sending_text(0), % Immediate send
	   plog(sent(text))
	; true % No leak, or already reported within the last hour
	),
        leak_color(Op, Color),
	paint(spacer, Color).


reportDeltaT(Who, Old, New) :-
	DeltaT is integer(abs(Old-New)/10), % DeltaT in degrees
	retractall(err(Who,deltaT(_))),
	(DeltaT > 5 -> assert(err(Who,deltaT(DeltaT))) ; true).

reportTurb(Who,What,Target,Turb) :- 
	condition(Turb, Target, 50, Cond),
	retractall(turbidity(Who,What,   _,   _)),
	assert(    turbidity(Who,What,Cond,Turb)).

condition(Value,Target,Delta,Cond) :-
	LowThresh is Target - Delta,
	HighThresh is Target + Delta,
	( Value < LowThresh   -> Cond = low
	 ;(Value > HighThresh -> Cond = high
	 ;                       Cond = okay)).

:- pce_begin_class(spacer, box). % SECTION SEPARATOR
variable(defaultColor, name := black, both, "Default Color" ).
initialise(Self, _Name:[name]) :->
        screen(DW,_,WW,_,_),
	NWid is DW*WW/110,
        send_super(Self, initialise(NWid, 6)),
        get(Self, defaultColor, C),
	send(Self, colour, colour(C)),
	send(Self, fill_pattern, colour(C)).

color(Self, Color) :->
     ( get(Self,defaultColor,black)         % First time
      -> send(Self,slot,defaultColor,Color) % New default
      ;  true
     ),
     send(Self, colour, colour(Color)),
     send(Self, fill_pattern, colour(Color)).

resetColor(Self) :-> 
     get(Self, defaultColor,C),
     send(Self, colour, colour(C)).

update(_Self) :-> true.

closesocket(_Self) :-> true.

:- pce_end_class. % spacer

:- pce_begin_class(hotbox, box).
initialise(Self, Color:[name]) :->
        "Initialise"::
        window_percent(1,2,Width,Height),
        send_super(Self, initialise(Width,Height)),
	send(Self, colour, colour(black)),
        send(Self, fill_pattern, colour(Color)).
	
closesocket(_Self) :-> true.

:- pce_end_class.  % hotbox

% Arduino interface objects [cellstat,lagoon, etc.]
:- tell(iface), [ard], told.   % Create Interface classes (> iface)
:- [iface].                    % CONSULT
:- [dialin].   % Pop up Arduino dialog
:- [prephtml]. % prepare PCE data for web

